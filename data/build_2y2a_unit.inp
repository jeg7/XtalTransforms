* build_2y2a_unit.inp: Generates a PSF and coordinate file for 2y2a unit cell
* jeg240919
*
iofo exte

! Number of segments in the unit cell
set nxtal 4

! Protein name and charmm directory
set M 2y2a
set chm_dir /home/jeg/sw/charmm/c49a10

! Topology and parameter files
read rtf card name @{chm_dir}/toppar/top_all36_prot.rtf
read para card flex name @{chm_dir}/toppar/par_all36m_prot.prm
stream @{chm_dir}/toppar/toppar_water_ions.str

! Read sequence card for single 4oph segment
read sequ card
* 2y2a sequence
*
6
LYS LEU VAL PHE PHE ALA

! Generate original PSF
gene setup A000 first NTER last CTER

! Duplicate segment for the rest of the molecules in the unit cell
set i 1
label L0
if @i .lt. 10 then
gene A00@i dupl A000 setup
else
if @i .lt. 100 then
gene A0@i dupl A000 setup
else
if @i .lt. 1000 then
gene A@i dupl A000 setup
endif
endif
endif
incr i by 1
if @i .lt. @{nxtal} goto L0

! Renumber residues
set i 0
label L1
set j 6
set k 7
label L2
if @i .lt. 10 then
rename resi @k sele segi A00@i .and. resi @j end
else
if @i .lt. 100 then
rename resi @k sele segi A0@i .and. resi @j end
else
if @i .lt. 1000 then
rename resi @k sele segi A@i .and. resi @j end
endif
endif
endif
decr j by 1
decr k by 1
if @j .ge. 1 goto L2
incr i by 1
if @i .lt. @{nxtal} goto L1

! Read coordinates
! Here we can ignore the warnings about TRP not having the "O" atom since we are
! adding the CTER patch to it
read coor pdb resi name @M_unit.pdb

! Fill missing coordinates and add hydrogens
ic purge
ic param
ic fill pres
ic build
define test sele ( .not. type H* ) .and. ( .not. init ) show end
hbuild sele type H* end

! Write PSF and coordinates
write psf card name @M_unit.psf
* @M unit cell
*

write coor card name @M_unit.cor
* @M unit cell
*

stop
